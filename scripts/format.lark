// A bunch of WORDS
start: header_block ingredients_block instructions_block tags_block?

header_block: title source servings
ingredients_block: ingredients_header ingredients
instructions_block: steps (_DIVIDER notes)?
tags_block: tag_header tags tag_footer

title: "#" WORDS _NEWLINES?
source: "<small>" "Recipe by:" WORDS "</small>" _NEWLINES?
servings: "<small>" "Servings:" WORDS "</small>" _NEWLINES?

ingredients_header: "|" "Amount" "|" "Ingredient" "|" "Prep Notes" "|" _NEWLINES? "|" "-"+ "|" ":" "-"+ "|" ":" "-"+ "|" _NEWLINES?
ingredients: ingredient+ _NEWLINES?
ingredient: "|" ingredient_amount? "|" ingredient_name? "|" ingredient_notes? "|" _NEWLINES
ingredient_amount: quantity? measurement?
ingredient_name: WORDS
ingredient_notes: WORDS

steps: step (_NEWLINES step)* _NEWLINES?
step: "- " WORDS

notes: note (_NEWLINES note)* _NEWLINES?
note: "- " "_note_:" WORDS

tag_header: "<!--" "Tags:" _NEWLINES
tags: tag+ _NEWLINES?
tag: "-" WORDS _NEWLINES
tag_footer: "-->" _NEWLINES?


quantity: FRACTION
measurement: AMOUNT | WORDS

WORDS: ((APOSTROPHE_WORD | NUMBER | PUNCTUATION) WS_INLINE?)+


FRACTION: (NUMBER WS_INLINE)? NUMBER "/" NUMBER | NUMBER
APOSTROPHE_WORD: WORD ("'" WORD)?
_DIVIDER: "---" _NEWLINES?
_NEWLINES: NEWLINE+
PUNCTUATION: /[^\s\w\n<>|]|_/
AMOUNT: "pinch"

%import common.WORD
%import common.NUMBER  
%import common.NEWLINE
%import common.WS_INLINE

%ignore WS_INLINE          